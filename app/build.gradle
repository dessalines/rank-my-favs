plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'com.google.devtools.ksp'
}

android {
    buildToolsVersion = "34.0.0"
    compileSdk 34

    defaultConfig {
        applicationId "com.dessalines.rankmyfavs"
        minSdk 21
        targetSdkVersion 34
        versionCode 14
        versionName "0.3.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ksp { arg('room.schemaLocation', "$projectDir/schemas") }
    }

    // Necessary for izzyondroid releases
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD

                // Optional, specify signing versions used
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' (DEBUG)'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ['-Xjvm-default=all-compatibility', '-opt-in=kotlin.RequiresOptIn']
    }
    buildFeatures {
        compose true
    }
    namespace 'com.dessalines.rankmyfavs'
}

composeCompiler {
    enableStrongSkippingMode = true
}

dependencies {
    // CSV exporting
    implementation 'com.floern.castingcsv:casting-csv-kt:1.2'

    // Tables
    implementation 'com.github.Breens-Mbaka:BeeTablesCompose:1.2.0'

    // Glicko2
    implementation 'com.github.goochjs:glicko2:master'

    // Compose BOM
    implementation platform('androidx.compose:compose-bom:2024.08.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended-desktop:1.6.8'
    implementation 'androidx.compose.material3:material3-window-size-class'
    implementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.compose.runtime:runtime-livedata:1.6.8'

    // Activities
    implementation 'androidx.activity:activity-compose:1.9.1'
    implementation 'androidx.activity:activity-ktx:1.9.1'

    // LiveData
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.8.4'

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.7.7'

    // Markdown
    implementation "com.github.jeziellago:compose-markdown:0.5.2"

    // Preferences
    implementation 'me.zhanghai.compose.preference:library:1.0.0'

    // Room
    // To use Kotlin annotation processing tool
    ksp 'androidx.room:room-compiler:2.6.1'
    implementation 'androidx.room:room-runtime:2.6.1'
    annotationProcessor 'androidx.room:room-compiler:2.6.1'

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation 'androidx.room:room-ktx:2.6.1'

    // App compat
    implementation 'androidx.appcompat:appcompat:1.7.0'
}
